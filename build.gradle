buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.0.5'
    }
}

plugins {
    id 'com.google.cloud.tools.jib' version '3.3.1'
}

version = '1.0.3'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {  //aplikator
            url 'https://gitlab.com/api/v4/projects/18272083/packages/maven'
        }
    }
    apply plugin: 'java'
    compileJava   {
        options.encoding = 'UTF-8'
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }
}

subprojects {
    apply plugin: 'gwt'
}


apply plugin: 'war'

dependencies {
    compile "org.aplikator:aplikator:1.0.6"
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    runtime "org.postgresql:postgresql:42.7.2"
    compile "org.apache.poi:poi:3.14"
    compile "org.apache.poi:poi-ooxml:3.14"
    compile "commons-codec:commons-codec:1.10"
}

war {
    from project(':razitka-client').tasks.compileGwt.outputs
    archiveName = "razitka.war"
}


jib {
    from {
        image = 'bitnami/tomcat:9.0.75'
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {
        image = "ghcr.io/inovatika-cz/razitka:${version}"
        auth {
            username = findProperty('githubRepositoryUser') ?: System.getenv('GITHUB_ACTOR')
            password = findProperty('githubRepositoryToken') ?: System.getenv('GITHUB_TOKEN')
        }
    }
    container {
        appRoot = '/opt/bitnami/tomcat/webapps/razitka'
        user = '1001'
    }
}
