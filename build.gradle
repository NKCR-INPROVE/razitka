buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.google.cloud.tools.jib' version '3.3.1'
}

version = '1.0.3'

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {  //aplikator
            url 'https://gitlab.com/api/v4/projects/18272083/packages/maven'
        }
    }
    apply plugin: 'java'
    compileJava   {
        options.encoding = 'UTF-8'
        sourceCompatibility = "17"
        targetCompatibility = "17"
    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

apply plugin: 'war'

dependencies {
    implementation "org.aplikator:aplikator:1.1.4"
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'

    runtimeOnly "org.postgresql:postgresql:42.7.7"
    implementation "org.apache.poi:poi:3.14"
    implementation "org.apache.poi:poi-ooxml:3.14"
    implementation "commons-codec:commons-codec:1.10"
}


evaluationDependsOn(':razitka-client')
war {
    dependsOn project(':razitka-client').tasks.named('gwtCompile')
    from(project(':razitka-client').layout.buildDirectory.dir("gwt"))
    archiveFileName = "razitka.war"
}


jib {
    from {
        image = 'bitnami/tomcat:9.0.75'
        platforms {
            platform {
                architecture = 'amd64'
                os = 'linux'
            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {
        image = "ghcr.io/inovatika-cz/razitka:${version}"
        auth {
            username = findProperty('githubRepositoryUser') ?: System.getenv('GITHUB_ACTOR')
            password = findProperty('githubRepositoryToken') ?: System.getenv('GITHUB_TOKEN')
        }
    }
    container {
        appRoot = '/opt/bitnami/tomcat/webapps/razitka'
        user = '1001'
    }
}
